group 'io.github.mictaege'
version '2024.2-rc2'

apply plugin: 'java'
apply plugin: 'jitter'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'org.openjfx.javafxplugin'

java {
    sourceCompatibility = 21
    targetCompatibility = 21
}


wrapper {
    gradleVersion = "8.7"
    distributionType = Wrapper.DistributionType.ALL
}

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'io.github.mictaege', name: 'jitter-plugin', version:'2024.2-rc2'
        classpath 'org.openjfx:javafx-plugin:0.1.0'
    }
}

repositories {
    mavenCentral()
}

sourceSets {
    model {
        java {
            srcDir 'src/model/java'
        }
        resources {
            srcDir 'src/model/resources'
        }
    }
    modelTest {
        compileClasspath += model.runtimeClasspath
        runtimeClasspath += model.runtimeClasspath
        java {
            srcDir 'src/modelTest/java '
        }
        resources {
            srcDir 'src/modelTest/resources'
        }
    }
    beans {
        compileClasspath += model.runtimeClasspath
        runtimeClasspath += model.runtimeClasspath
        java {
            srcDir 'src/beans/java'
        }
        resources {
            srcDir 'src/beans/resources'
        }
    }
    beansTest {
        compileClasspath += beans.runtimeClasspath + modelTest.runtimeClasspath
        runtimeClasspath += beans.runtimeClasspath + modelTest.runtimeClasspath
        java {
            srcDir 'src/beansTest/java'
        }
        resources {
            srcDir 'src/beansTest/resources'
        }
    }
    ui {
        compileClasspath += beans.runtimeClasspath
        runtimeClasspath += beans.runtimeClasspath
        java {
            srcDir 'src/ui/java'
        }
        resources {
            srcDir 'src/ui/resources'
        }
    }
    uiTest {
        compileClasspath += ui.runtimeClasspath + beansTest.runtimeClasspath
        runtimeClasspath += ui.runtimeClasspath + beansTest.runtimeClasspath
        java {
            srcDir 'src/uiTest/java'
        }
        resources {
            srcDir 'src/uiTest/resources'
        }
    }
    main {
        compileClasspath += ui.runtimeClasspath
        runtimeClasspath += ui.runtimeClasspath
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
    test {
        compileClasspath += main.runtimeClasspath + uiTest.runtimeClasspath
        runtimeClasspath += main.runtimeClasspath + uiTest.runtimeClasspath
        java {
            srcDir 'src/test/java'
        }
        resources {
            srcDir 'src/test/resources'
        }
    }
}

tasks.compileJava.dependsOn tasks.compileUiJava
tasks.compileUiJava.dependsOn tasks.compileBeansJava
tasks.compileBeansJava.dependsOn tasks.compileModelJava

tasks.compileTestJava.dependsOn tasks.compileUiTestJava
tasks.compileUiTestJava.dependsOn tasks.compileBeansTestJava
tasks.compileBeansTestJava.dependsOn tasks.compileModelTestJava

dependencies {
    modelImplementation "io.github.mictaege:jitter-api:2024.2-rc2"
    modelTestImplementation "org.junit.jupiter:junit-jupiter-api:5.10.2"
    modelTestImplementation "org.junit.jupiter:junit-jupiter-engine:5.10.2"
    modelTestImplementation "org.mockito:mockito-core:5.11.0"
    modelTestImplementation "org.concordion:concordion:4.0.1"
}

javafx {
    modules = [ 'javafx.controls', 'javafx.fxml' ]
    configurations = [ 'uiImplementation' ]
}

idea {
    module {
        inheritOutputDirs = false
        testSourceDirs += file('modelTest')
        testSourceDirs += file('beansTest')
        testSourceDirs += file('uiTest')
        outputDir file('build/classes/main')
        testOutputDir file('build/classes/test')
    }
}

mainClassName = "com.github.mictaege.eval.jitter.TowerControlMain"

jar {
    from sourceSets.model.output
    from sourceSets.beans.output
    from sourceSets.ui.output
    from sourceSets.main.output
}

run {
    classpath sourceSets.main.runtimeClasspath
}

jitter {

    flavour {
        name = 'ESA'
        criticalTerms {
            patterns = ['\\b(?i)(esa)\\b', '\\b(?i)(hermes)\\b']
            patterns << 'Construction Year'
            excludes << '**/Flavour.java'
            includes << 'scripts/**'
            sizeLimitKb = 200
        }
    }

    flavour {
        name = 'NASA'
        criticalTerms {
            patterns = ['\\b(?i)(nasa)\\b', '\\b(?i)(mercury)\\b', '\\b(?i)(gemini)\\b']
            excludes << '**/*.html'
            excludes << '**/Flavour.java'
        }
    }

    flavour {
        name = 'ROSKOSMOS'
        criticalTerms {
            patterns = ['\\b(?i)(roskomos)\\b', '\\b(?i)(wostok)\\b', '\\b(?i)(sojus)\\b']
            excludes << '**/Flavour.java'
            includes << 'scripts/**'
        }
    }

    excludeSrcSets = ['main', 'test']
    compliance = 11

}